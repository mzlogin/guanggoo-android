apply plugin: 'com.android.application'

def Properties mVersionProps = getVersionProperties()

def mVersionCode = getVersionCode()

def mVersionName = mVersionProps['MAJOR_VERSION_NAME'] + "." + mVersionCode


android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "org.mazhuang.guanggoo"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode mVersionCode
        versionName mVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [
                appChannel: "unknown"
        ]
    }

    productFlavors {
        playstore {}
        github {}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [
                appChannel : name
        ]
    }

    signingConfigs {
        debug {

        }
        release {
            def Properties signingProps = getSignProperties()
            if (signingProps != null) {
                storeFile file(signingProps['STORE_FILE'])
                storePassword signingProps['STORE_PASSWORD']
                keyAlias signingProps['KEY_ALIAS']
                keyPassword signingProps['KEY_PASSWORD']
            }
        }
    }

    buildTypes {
        debug {

        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (getSignProperties() != null) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = "guanggoo_${variant.productFlavors[0].name}_v${defaultConfig.versionName}_${variant.buildType.name}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
}

def getVersionProperties() {
    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file('../config/version.properties')))
    return versionProps
}

def getVersionCode() {
    return Integer.valueOf('git rev-list HEAD --count '.execute().text.trim())
}

def getSignProperties() {
    File signingPropertiesFile = file('../config/signing.properties')
    if (signingPropertiesFile.exists()) {
        def Properties signingProperties = new Properties()
        signingProperties.load(new FileInputStream(signingPropertiesFile))
        if (signingProperties.containsKey('STORE_FILE') &&
            signingProperties.containsKey('STORE_PASSWORD') &&
            signingProperties.containsKey('KEY_ALIAS') &&
            signingProperties.containsKey('KEY_PASSWORD')) {
            return signingProperties
        }
    }
    return null
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'org.jsoup:jsoup:1.10.3'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.jakewharton:butterknife:8.8.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'org.sufficientlysecure:html-textview:3.4'
    compile 'com.tencent.bugly:crashreport_upgrade:latest.release'
    testCompile 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    compile 'com.github.medyo:android-about-page:1.2.1'
}
