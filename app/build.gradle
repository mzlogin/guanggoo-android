apply plugin: 'com.android.application'

Properties mVersionProps = getVersionProperties()

def mVersionCode = getVersionCode()

def mVersionName = mVersionProps['MAJOR_VERSION_NAME'] + "." + mVersionCode


android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "org.mazhuang.guanggoo"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode mVersionCode
        versionName mVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [
                appChannel: "unknown"
        ]
        multiDexEnabled true
    }

    flavorDimensions 'channel'

    productFlavors {
        playstore {}
        github {}
        coolapk {}
        qq {}
        xiaomi {}
        three60 {}
    }

    productFlavors.all {
        flavor ->
            flavor.manifestPlaceholders = [
                    appChannel: name
            ]
    }

    signingConfigs {
        debug {

        }
        release {
            Properties signingProps = getSignProperties()
            if (signingProps != null) {
                storeFile file(signingProps['STORE_FILE'])
                storePassword signingProps['STORE_PASSWORD']
                keyAlias signingProps['KEY_ALIAS']
                keyPassword signingProps['KEY_PASSWORD']
            }
        }
    }

    buildTypes {
        debug {
            resValue "string", "build_config_type", "debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (getSignProperties() != null) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
            resValue "string", "build_config_type", "release"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "guanggoo_${variant.productFlavors[0].name}_v${defaultConfig.versionName}_${variant.buildType.name}.apk"
        }
    }
}

def getVersionProperties() {
    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file('../config/version.properties')))
    return versionProps
}

static def getVersionCode() {
    try {
        String output = 'git rev-list HEAD --count '.execute().text.trim()
        return Integer.valueOf(output)
    } catch (Exception e) {
        System.out.println(e.getMessage())
    }
    return 1
}

def getSignProperties() {
    File signingPropertiesFile = file('../config/signing.properties')
    if (signingPropertiesFile.exists()) {
        Properties signingProperties = new Properties()
        signingProperties.load(new FileInputStream(signingPropertiesFile))
        if (signingProperties.containsKey('STORE_FILE') &&
                signingProperties.containsKey('STORE_PASSWORD') &&
                signingProperties.containsKey('KEY_ALIAS') &&
                signingProperties.containsKey('KEY_PASSWORD')) {
            return signingProperties
        }
    }
    return null
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'org.jsoup:jsoup:1.10.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
    implementation 'org.sufficientlysecure:html-textview:3.6'
    implementation 'me.saket:better-link-movement-method:2.2.0'
    implementation 'com.tencent.bugly:crashreport_upgrade:1.3.5'
    testImplementation 'junit:junit:4.12'
    implementation 'com.github.medyo:android-about-page:1.2.1'
    implementation('com.vdurmont:emoji-java:4.0.0') { exclude group: 'org.json', module: 'json' }
    implementation 'com.bm.photoview:library:1.4.1'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    compileOnly 'org.projectlombok:lombok:1.18.2'
    annotationProcessor 'org.projectlombok:lombok:1.18.2'
    implementation 'androidx.multidex:multidex:2.0.1'
}
